# Maintainer: Ryan Roden-Corrent <ryan@rcorre.net>

_reponame=mc
_pkgname=myrddin
pkgname=myrddin-git
pkgver=0.3939.606bce2
pkgrel=1
pkgdesc='Compiler and tools for the Myrddin programming language.'
arch=('i686' 'x86_64')
license=('MIT')
url="https://myrlang.org/"
source=("git://git.eigenstate.org/git/ori/mc.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_reponame"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd "$srcdir/$_reponame"
  ./configure --prefix="$pkgdir/usr"
  make
}

package() {
  cd "$srcdir/$_reponame"
  make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$_pkgname/README.md"

  # avoid conflict with midnight commander
  # this manpage is for the compiler, called 6m
  mv "$pkgdir/usr/share/man/man1/"{mc,6m}.1
}
